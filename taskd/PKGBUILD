# Maintainer: Rich Li <rich at dranek com>
# Contributor: Kevin MacMartin <prurigro at gmail dot com>
# Contributor: Markus Holtermann <aur@markusholtermann.eu>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=taskd
pkgver=1.0.0
pkgrel=7
pkgdesc="A lightweight, secure server providing multi-user, multi-client access to task data"
url="http://tasktools.org/projects/${pkgname}.html"
license=('MIT')
arch=('i686' 'x86_64' 'arm' 'armv6h' 'armv7h')
depends=('gnutls' 'libutil-linux')
makedepends=('cmake')
backup=("etc/default/${pkgname}")
install="${pkgname}.install"
source=("http://taskwarrior.org/download/${pkgname}-1.0.0.tar.gz"
        "${pkgname}-fix-pki-generate-path.patch"
        "${pkgname}.service"
        "${pkgname}.default"
        "${pkgname}.tmpfiles.conf"
        "${pkgname}.sysusers.conf")

sha512sums=('e60a07fc0ae9fbebb9e05a7f0c21df9cf3ea39ce01570168c51e60b2ccb9cf59ab85fd35821e1298048ee669bfc40560e010c06ba85d794272b897897e6d956b'
            'c01f31f641fb9ea5183bbe3b87858824f21f049ebe1560c0de672f06b1f0deaca41eba1e9ed1336e15141edd72f9c87590249eea3756c45af8524d34efbad52a'
            '76ecb3d5488768bb90340c0751a4ea6d753021d498d1258d6a91450a02e5e49e7971400164f5d997099b9bf0b2d27942b5c94f75764c711a4b4e82303aed818e'
            'e05ff87aae4eb81d7d8c69396653abeef4d35be6804c7547440888078b33b77ed00371c10429453d59bf1583fb53e42e8f7b4120bac84aa9a29b046190aae44d'
            '2b17c1262d061a79f79077c408d71d48a178a742fbda25a2c8519e052b192ea4054dd379f40de55b34169993c2c52811d6b461f4baec0c538724898942b87bff'
            '882ffe7de4bf569a16595d69cfd827fffd23beae7af19c90748bc2ee23e74f8313bcd2733131155d8fea4a37a207b052255fa5aeb6b61b47e4207332f7083e04')

prepare() {
    patch -p0 < ${pkgname}-fix-pki-generate-path.patch
}

build() {
    cd ${pkgname}-${pkgver}
    cmake . \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release
    make
}

# Doesn't exist in 1.0.0 tarball but it does in git...
# check() {
#     cd ${pkgname}-${pkgver}/test
#     make
#     ./run_all
# }

package() {
    install -Dm644 ${pkgname}.default "${pkgdir}/etc/default/${pkgname}"
    install -Dm644 ${pkgname}.tmpfiles.conf "${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf"
    install -Dm644 ${pkgname}.sysusers.conf "${pkgdir}/usr/lib/sysusers.d/${pkgname}.conf"
    install -Dm644 ${pkgname}.service "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"

    cd ${pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    install -d "${pkgdir}/usr/share/${pkgname}/pki"
    install -D pki/generate{,.ca,.client,.crl,.server} "${pkgdir}/usr/share/${pkgname}/pki/"
}
